# Copyright (c) 2023-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/notification/common_event_service/event.gni")
import("//build/config/components/idl_tool/idl.gni")

idl_gen_interface("static_subscriber_interface") {
  sources = [ "IStaticSubscriber.idl" ]
  log_domainid = "0xD001202"
  log_tag = "Ces"
  subsystem_name = "notification"
  part_name = "common_event_service"
}

config("static_subscriber_ipc_public_config") {
  visibility = [ ":*" ]

  include_dirs = [ "${target_gen_dir}" ]
}

ohos_shared_library("static_subscriber_ipc") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  include_dirs = []

  sources = []

  configs = []
  public_configs = [ ":static_subscriber_ipc_public_config" ]

  deps = [
    ":static_subscriber_proxy",
    ":static_subscriber_stub",
    "${ces_native_path}:cesfwk_innerkits",
  ]

  external_deps = [
    "ability_base:want",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
  ]

  subsystem_name = "notification"
  part_name = "common_event_service"
}

config("static_subscriber_extension_public_config") {
  visibility = [ ":*" ]

  include_dirs = [ "${ces_extension_path}/include" ]
}

ohos_shared_library("static_subscriber_extension") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  include_dirs = []

  sources = [
    "${ces_extension_path}/src/js_static_subscriber_extension.cpp",
    "${ces_extension_path}/src/js_static_subscriber_extension_context.cpp",
    "${ces_extension_path}/src/static_subscriber_extension.cpp",
    "${ces_extension_path}/src/static_subscriber_extension_context.cpp",
    "${ces_extension_path}/src/static_subscriber_stub_impl.cpp",
  ]

  configs = []
  public_configs = [ ":static_subscriber_extension_public_config" ]

  deps = [
    ":static_subscriber_ipc",
    "${ces_native_path}:cesfwk_innerkits",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:app_context",
    "ability_runtime:extensionkit_native",
    "ability_runtime:napi_common",
    "ability_runtime:runtime",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_core",
    "ipc:ipc_napi",
    "napi:ace_napi",
  ]

  subsystem_name = "notification"
  part_name = "common_event_service"
}

config("static_subscriber_extension_module_public_config") {
  visibility = [ ":*" ]

  include_dirs = [ "${ces_extension_path}/include/loader" ]
}

ohos_shared_library("static_subscriber_extension_module") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  include_dirs = [ "${ces_core_path}/include" ]

  sources = [ "${ces_extension_path}/src/loader/static_subscriber_extension_module_loader.cpp" ]

  configs = []
  public_configs = [ ":static_subscriber_extension_module_public_config" ]

  deps = [ ":static_subscriber_extension" ]

  external_deps = [
    "ability_base:configuration",
    "ability_base:session_info",
    "ability_runtime:ability_manager",
    "ability_runtime:extensionkit_native",
    "ability_runtime:runtime",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "ipc:ipc_napi",
    "napi:ace_napi",
  ]

  relative_install_dir = "extensionability"
  subsystem_name = "notification"
  part_name = "common_event_service"
}

group("cesfwk_extension") {
  deps = [
    ":static_subscriber_extension",
    ":static_subscriber_extension_module",
    ":static_subscriber_ipc",
  ]
}

ohos_source_set("static_subscriber_proxy") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":static_subscriber_ipc_public_config" ]
  output_values = get_target_outputs(":static_subscriber_interface")
  sources = filter_include(output_values, [ "*_proxy.cpp" ])
  deps = [
    ":static_subscriber_interface",
    "${ces_native_path}:cesfwk_innerkits",
  ]
  external_deps = [
    "ability_base:want",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
  ]
  subsystem_name = "notification"
  part_name = "common_event_service"
}

ohos_source_set("static_subscriber_stub") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":static_subscriber_ipc_public_config" ]
  output_values = get_target_outputs(":static_subscriber_interface")
  sources = filter_include(output_values, [ "*_stub.cpp" ])
  deps = [
    ":static_subscriber_interface",
    "${ces_native_path}:cesfwk_innerkits",
  ]
  external_deps = [
    "ability_base:want",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
  ]
  subsystem_name = "notification"
  part_name = "common_event_service"
}
